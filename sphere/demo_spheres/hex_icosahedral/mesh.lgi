*
* Create a hexahedral icosahedral sphere mesh
* Number of nodes per shell is 642
*

define / r1 / 1.0
define / r2 / 2.0
define / r3 / 3.0

geometry / create / geom_sph_shells
cmo / create / dummy
cmo / geometry / dummy / geom_sph_shells

surface/sphere_r1/intrface/sphere/0.0,0.0,0.0/r1/
surface/sphere_r2/intrface/sphere/0.0,0.0,0.0/r2/
surface/sphere_r3/reflect /sphere/0.0,0.0,0.0/r3/
surface/boundbox/reflect /box/-4.,-4.,-4./4.,4.,4.

region /r_r1   /lt sphere_r1/
region /r_r1_r2/ge sphere_r1 and lt sphere_r2/
region /r_r2_r3/ge sphere_r2 and le sphere_r3/

mregion/m_r1   /lt sphere_r1/
mregion/m_r1_r2/gt sphere_r1 and lt sphere_r2/
mregion/m_r2_r3/gt sphere_r2 and le sphere_r3/



define / n_icos / 642

cmo/create/cmo1/ / / hex
cmo / geometry / cmo1 / geom_sph_shells
cmo / constraint / cmo1 / dummy
createpts/sphere/8/2/n_icos/r1/r2/0.0,0.0,0.0/1,0,0.0/
createpts/sphere/8/3/n_icos/r2/r3/0.0,0.0,0.0/1,0,0.0/
filter / 1 0 0
rmpoint / compress
setpts
settets
dump/gmv/icosahedron_642.gmv/cmo1/
dump / avs / icosahedron_642.inp / cmo1
cmo / status / cmo1
cmo / delete / cmo1

finish
